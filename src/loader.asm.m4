
RESTART	EQU	$0000			; CP/M & MSX-DOS RESTART VECTOR

	ORG	$100

	; LD	HL, SP			; CAPTURE CURRENT HI MEMORY INTO HL
	; DI				; DISABLE INTERRUPTS AS SP IS NOT SAFE YET
	; DEC	HL			; PROTECT TOP MEM
	LD	HL, $BFFF		; PLACE APP AT TOP OF PAGE 2 - PAGE 3 HAS SP AND MSXBIOS STUFF

	LD	L, 0
	LD	DE, PADDING
	ADD	HL, DE

	PUSH	HL
	POP	IX
	LD	DE, HL

	LD	HL, APPEND - 1
	LD	BC, APPSIZE
	LDDR

	EX	DE, HL
	; LD	SP, HL			; ASSIGN STACK TO JUST BELOW RELOCATED APP - ITS NOW SAFE TO USE

	INC	HL
	PUSH	HL			; CAPTURE START POINT IN IX
	POP	IX

	LD	DE, $100		; ADJUST ADDRESS AS PER RELOCATION BITMAP
	OR	A
	SBC	HL, DE
	EX	DE, HL

	PUSH	IX

	LD	HL, RELOC + 2
	LD	BC, (RELOC)

LOOP:
	CALL	TRANSPOSE

	DEC	BC
	LD	A, B
	OR	C
	JR	NZ, LOOP

	POP	IX
	; EI				; RE-ENABLE INTERRUPTS

	JP	(IX)

TRANSPOSE:
	PUSH	BC

	LD	B, 8
	LD	A, (HL)
NEXT:
	RLCA
	JR	NC, SKIP

	EX	AF, AF'
	LD	A, (IX)
	ADD	A, D
	LD	(IX), A
	EX	AF, AF'

SKIP:
	INC	IX
	DEC	B
	JR	NZ, NEXT

	POP	BC
	INC	HL
	RET

RELOC:
BINARY	"./bin/relocation-map.bin"
RELOCSIZE:	EQU	ASMPC - RELOC


APP:
BINARY "./bin/SRC()100.bin"

APPEND:
APPSIZE:	EQU	ASMPC - APP

if (((ASMPC - APP) % 256))
PADDING:	EQU 	((ASMPC - APP) % 256) - 257
else
PADDING		EQU	-1
endif
